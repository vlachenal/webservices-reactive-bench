plugins {
  id "org.springframework.boot" version "2.1.7.RELEASE"
  id "net.ltgt.apt" version "0.15"
  id 'jacoco'
}

apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'
apply plugin: 'net.ltgt.apt'

war {
  enabled = true
}

configurations {
  mapstruct
}

eclipse {
  wtp {
    facet {
      facet name: 'jst.java', version: '11'
      facet name: 'jst.web', version: '4.0'
      def oldJstWebFacet = facets.findAll {
        it.name == 'jst.web' && it.version == '2.4' || it.name == 'jst.java' && it.version == '1.8' || it.name == 'jst.java' && it.version == '1.9' || it.name == 'jst.java' && it.version == '1.10'
      }
      facets.removeAll(oldJstWebFacet)
    }
  }
}

jar {
  baseName = 'webservices-reactive-bench'
  version =  '0.1.0'
}

bootRun {
  jvmArgs = ['--add-modules','java.xml.bind,java.xml.ws']
}

repositories {
  mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11

allprojects {
  sourceCompatibility = 11
  eclipse.project {
    natures 'org.eclipse.buildship.core.gradleprojectnature'
  }
  // Allows to run JUnit tests in Eclipse and have compilation warning for Mapstruct +
  project.plugins.withId("net.ltgt.apt") {
    // automatically apply net.ltgt.apt-eclipse whenever net.ltgt.apt is used
    project.apply plugin: "net.ltgt.apt-eclipse"
  }
  // Allows to run JUnit tests in Eclipse and have compilation warning for Mapstruct -
}

tasks.withType(JavaCompile) {
  options.compilerArgs = [
          '-Amapstruct.suppressGeneratorTimestamp=true'
  ]
}

repositories {
  mavenCentral()
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
  }
}

check.dependsOn jacocoTestReport

ext {
  mapstructVersion = "1.3.0.Final"
}

dependencies {
  // REST
  compile 'org.springframework.boot:spring-boot-starter-webflux'
  // SQL
  compile 'org.springframework.boot:spring-boot-starter-jdbc'
  compile 'org.postgresql:postgresql:42.2.6'
  // MapStruct
  compile "org.mapstruct:mapstruct-jdk8:${mapstructVersion}"
  annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
  // Dozer
  compile 'com.github.dozermapper:dozer-core:6.5.0'
  // JAXB => needed by Dozer ...
  compile 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41'
  compile 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
  compile 'javax.xml.bind:jaxb-api:2.2.7'
  // tests
  testCompile 'org.springframework.boot:spring-boot-starter-test'
  testCompile 'io.projectreactor:reactor-test'
  testCompile 'org.hsqldb:hsqldb:2.5.0'
}
